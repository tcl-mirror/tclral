[manpage_begin relvar n 0.8.5]
[comment {
# This software is copyrighted 2004, 2005, 2006, 2007 by G. Andrew Mangogna.
# The following terms apply to all files associated with the software unless
# explicitly disclaimed in individual files.
# 
# The authors hereby grant permission to use, copy, modify, distribute,
# and license this software and its documentation for any purpose, provided
# that existing copyright notices are retained in all copies and that this
# notice is included verbatim in any distributions. No written agreement,
# license, or royalty fee is required for any of the authorized uses.
# Modifications to this software may be copyrighted by their authors and
# need not follow the licensing terms described here, provided that the
# new terms are clearly indicated on the first page of each file where
# they apply.
# 
# IN NO EVENT SHALL THE AUTHORS OR DISTRIBUTORS BE LIABLE TO ANY PARTY FOR
# DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES ARISING
# OUT OF THE USE OF THIS SOFTWARE, ITS DOCUMENTATION, OR ANY DERIVATIVES
# THEREOF, EVEN IF THE AUTHORS HAVE BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE AUTHORS AND DISTRIBUTORS SPECIFICALLY DISCLAIM ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE, AND NON-INFRINGEMENT.  THIS SOFTWARE
# IS PROVIDED ON AN "AS IS" BASIS, AND THE AUTHORS AND DISTRIBUTORS HAVE
# NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES, ENHANCEMENTS,
# OR MODIFICATIONS.
# 
# GOVERNMENT USE: If you are acquiring this software on behalf of the
# U.S. government, the Government shall have only "Restricted Rights"
# in the software and related documentation as defined in the Federal
# Acquisition Regulations (FARs) in Clause 52.227.19 (c) (2).  If you
# are acquiring the software on behalf of the Department of Defense,
# the software shall be classified as "Commercial Computer Software"
# and the Government shall have only "Restricted Rights" as defined in
# Clause 252.227-7013 (c) (1) of DFARs.  Notwithstanding the foregoing,
# the authors grant the U.S. Government and others acting in its behalf
# permission to use and distribute the software in accordance with the
# terms specified in this license.
# 
#  *++
# MODULE:
#   relvar.man -- documentation for the ::ral::relvar command
# ABSTRACT:
#   This file contains the documentation for the ::ral::relvar command.
#   This command is part of TclRAL. This file is in Tcl Markup Language
#   format.
# $RCSfile: relvar.man,v $
# $Revision: 1.22 $
# $Date: 2008/01/10 16:48:16 $
#  *--
}]
[moddesc "Relvar Operators"]
[titledesc "Operators for Relation Variables"]
[copyright "2004, 2005, 2006, 2007 by G. Andrew Mangogna"]
[require ral [opt 0.8.5]]
[description]
This manpage describes the [cmd relvar] command.
The [cmd relvar] command is part of the Tcl Relational Algebra Library
(ral) package.

[para]
TclRAL defines a variable space for relation variables.
Relation variables hold relation values and the subcommands of the
[cmd relvar] command generally operate directly on the values storing
the results back into the relation variables.
For this reason, most of the [cmd relvar] subcommands
take the name of a relation variable as an argument.

[para]
In the relational model as it applies to database management systems,
relation variables play the important role of
representing the persistent data base values.
Relation variables are the leaves of the
expression tree that perform the desired query and represent
the core data from which results may be computed.
That computation is usually expressed as a single, sometimes complex expression.
This allows for a non-procedural way of specifying the computation and
allows optimizers to find the most efficient manner of computing the result.

[para]
In the TclRAL implementation of the relational model,
relation variables serve a similar role although there
is no implicit persistence supplied by the library.
Relation values may be stored in Tcl variables like any other Tcl object.
This does abandon the non-procedural nature of relational expressions
as usually accomplished in the relational model, but is a better fit
to the Tcl way of doing things.

[section "RELVAR NAMES"]

The namespace of relvar names follows the familiar Tcl method of naming
ordinary Tcl variables.
Thus relvars have Tcl namespace-like names.
For example, a relvar named [var Dog] created at the global level
will be named [var ::Dog].
Thus it is possible and indeed encouraged to group related relvars
together in a namespace.
If an application uses only one set of relvars,
then the global namespace of relvars is most convenient.
Using several distinct sets of relvars is best accomplished if each
set is placed in their own relvar namespace.

[para]
Since it is possible to store a relation value in an ordinary Tcl variable
and since Tcl variables are convenient to use when formulating expressions,
creating a relvar also creates a corresponding Tcl variable by the same name.
The Tcl variable created is traced to prevent writing to it, since all
modifications to a relvar should come via the [cmd relvar] commands.
So, creating a relvar name [var Dog] at the global level creates a
relvar named [var ::Dog] and a read-only Tcl variable named [var ::Dog].

[section CONSTRAINTS]

TclRAL supports the notion of [emph constraints] as a means of
insuring integrity among a set of relvars.
There are many types of constraints that might be used to insure integrity,
but TclRAL supports three types of declarative constraints.
[list_begin definitions]

[lst_item Association]
Association constraints define a referential association between two relvars.
In this context,
an association declares that a set of attributes in one relvar refers to
an identifier of another relvar.
Thus for all the tuples in a given relvar,
the given attribute values must match the corresponding attribute values
of the identifier of the associated relvar.
The association also declares the number of times that such matches may
occur and if it is allowed not to match at all.

[lst_item Partition]
Partition constraints define a complete and disjoint
referential partitioning between one relvar,
known as the super set,
and a given set of relvars,
known as the sub sets.
This constraint declares that every tuple of the super set relvar is
referred to by exactly one tuple from among all the tuples in the
sub set relvars.

[lst_item Correlation]
Correlation constraints define a referential association between two
relvars that is mediated by a third relvar.
The third relvar, the correlation relvar, contains attributes that
reference identifiers in the two associated relvars.
It is similar in some ways to two Association Constraints.

[list_end]

At the end of each [cmd ::ral::eval] command (see below) or,
if outside of an [cmd eval] command,
at the end of any command that modifies a relvar
that participates in a constraint,
all of the constraints defined for the modified relvars are evaluated.
If any of the constraints fail, the relvar modifications are discarded
and the relvar values are returned to the values they had before
the modifications.
Similarly,
during the creation of a constraint, the constraint is evaluated
on all the relvars associated with the constraint.
If the evaluation fails,
the constraint creation fails.

Like relvars, constraints have names.
Constraints names follow the same pattern as relvar names, i.e.
unresolved names are created in the current namespace.
Thus you are able to keep the relvars and their constraints organized
together in namespace-like groups.

[section COMMANDS]
[list_begin definitions]
[call [cmd "::ral::relvar association"] [arg name]\
    [arg refrngRelvar] [arg refrngAttrList] [arg refToSpec]\
    [arg refToRelvar] [arg refToAttrList] [arg refrngSpec]\
]

The [cmd association] subcommand creates a referential association
between two relvars called [arg name].
The return value of [cmd association] is the empty string.
Associations declare that one relvar has attributes that refer to
an identifier of another relvar.
The referring relvar name is given by the [arg refrngRelvar] argument
and the referring attributes are given by [arg refrngAttrList] argument.
The referred to relvar name is given by [arg refToRelvar] and the
attributes that are referred to by [arg refToAttrList]. The referring
and referred to attribute lists are ordered and referring attributes
refer to the corresponding referred to attribute.
The set of attributes given by [arg refToAttrList] must constitute
an identifier for [arg refToRelvar].
The [arg refToSpec] argument defines
the multiplicity and conditionality of the reference.
It can be one of:

[list_begin definitions]

[lst_item *]
Each tuple in [arg refToRelvar] is referred to zero or more times by
the tuples in [arg refrngRelvar].

[lst_item +]
Each tuple in [arg refToRelvar] is referred to one or more times by
the tuples in [arg refrngRelvar].

[lst_item 1]
Each tuple in [arg refToRelvar] is referred to exactly once by
the tuples in [arg refrngRelvar].

[lst_item ?]
Each tuple in [arg refToRelvar] is referred to zero or one times by
the tuples in [arg refrngRelvar].

[list_end]

Similarly [arg refrngSpec] defines the multiplicity and conditionality
associated with the references to [arg refToRelvar].
Necessarily, [arg refrngSpec] may only be "1" or "?" implying:
[list_begin definitions]

[lst_item 1]
Each tuple in [arg refrngRelvar] refers to exactly one tuple in
[arg refToRelvar].

[lst_item ?]
Each tuple in [arg refrngRelvar] refers to at most one tuple
(and possibly none at all) in [arg refToRelvar].

[list_end]

The symbols for [arg refrngSpec] and [arg refToSpec] were chosen
by analogy to the use of those symbols in regular expression syntax.

[example {
% relvar association A1 OWNERSHIP OwnerName + OWNER OwnerName 1
% relvar association A2 OWNERSHIP DogName * DOG DogName 1
}]

In this example,
A1 declares that for every tuple in OWNERSHIP,
the value of OWNERSHIP.OwnerName matches the value of OWNER.OwnerName
in exactly one tuple of OWNER and that for every tuple in OWNER the
value of OWNER.OwnerName matches the value of OWNERSHIP.OwnerName in
at least one tuple of OWNERSHIP and possibly more.
In the semantics of this example, the concepts of being an OWNER
and that of OWNERSHIP of a DOG are unconditionally associated since
no tuple of OWNER may exist without there being at least one associated
tuple of OWNERSHIP.
Also this constraint allows for OWNERS that may own more than one DOG.

Compare this to the case of A2.
A2 states that every tuple in OWNERSHIP must have a corresponding tuple
in DOG, but that tuples in DOG may not be referenced by any tuple in OWNERSHIP.
OWNERSHIP and DOG are conditionally associated
and in the semantics of this problem, it is possible to have dogs that 
are not owned at all and to have dogs that are owned multiple times.

[call [cmd "::ral::relvar constraint delete"]\
	[opt "[arg name1] [arg name2] ..."]]
The [cmd "constraint delete"] subcommand deletes all the
constraints given by the [arg nameN] arguments.
The return value is the empty string.

[call [cmd "::ral::relvar constraint info"] [arg name]]
The [cmd "constraint info"] subcommand returns the detailed information
about the constraint named [arg name].
The information returned is of the same format as the command used
to create the constraint minus the superfluous leading [cmd relvar] command
word.
Additionally,
the relvar names and constraint names are returned in fully qualified form.

[example {
% relvar constraint info A1
association ::A1 ::OWNERSHIP OwnerName + ::OWNER OwnerName 1
% relvar constraint info ::P1
partition ::P1 ::Lamp SerialNo ::TableLamp SerialNo ::FloorLamp SerialNo
% relvar constraint info G
unknown constraint name, "G"
}]

[call [cmd "::ral::relvar constraint names"] [opt [arg pattern]]]

The [cmd "constraint names"] subcommand returns a list of constraint names.
If the optional [arg pattern] is present,
then only those constraint names that match [arg pattern] are returned.
Name matching happens in the same manner as for the [cmd "string match"].
The returned names are fully qualified.

[example {
% relvar constraint names
::A1 ::A2
% relvar constraint names *2
::A2
}]

[call [cmd "::ral::relvar constraint member"] [arg relvarName]]

The [cmd "constraint member"] subcommand returns a list of constraint names
in which [arg relvarName] participates in some fashion.
The returned names are fully qualified.

[example {
% relvar constraint member OWNERSHIP
::A1 ::A2
}]

[call [cmd "::ral::relvar constraint path"] [arg constraintName]]
The [cmd "constraint path"] subcommand returns the fully qualified constraint
name of [arg constraintName].

[call [cmd "::ral::relvar correlation"] [opt -complete] [arg name]\
[arg correlRelvar]\
[arg correlAttrListA] [arg refToSpecA] [arg refToRelvarA] [arg refToAttrListA]\
[arg correlAttrListB] [arg refToSpecB] [arg refToRelvarB] [arg refToAttrListB]]

The [cmd correlation] subcommand creates a referential correlation
called [arg name].
The referential correlation is between a correlation relvar, [arg correlRelvar],
and two other relvars, [arg refToRelvarA] and [arg refToRelvarB].
The return value of [cmd correlation] is the empty string.

Correlations declare that every tuple in [arg correlRelvar] refers to
exactly one tuple of [arg refToRelvarA] and exactly one tuple of
[arg refToRelvarB].
The [arg correlAttrListA] attributes in [arg correlRelvar] refer to an
identifier of [arg refToRelvarA] that is given by the
attributes contained in the [arg refToAttrListA] list.
Similarly,
the [arg correlAttrListB] attributes in [arg correlRelvar] refer to an
identifier of [arg refToRelvarB] that is given by [arg refToAttrListB].
Both [arg refToAttrListA] and [arg refToAttrListB] must constitute
an identifier for their corresponding relvars.
The [arg refToSpecA] and [arg refToSpecB] arguments can have the same values
as for the [cmd "::ral::relvar association"] command and specify
the multiplicity and conditionality of how [arg refToRelvarA] and
[arg refToRelvarB] are referred to by the tuples of [arg correlRelvar].
If the [opt -complete] option is given,
then every tuple of [arg refToRelvarA] must be correlated to every
tuple of [arg refToRelvarB] and [emph "vice versa"].
That is, the cardinality of [arg correlRelvar] must equal the product
of the cardinality of [arg refToRelvarA] and [arg refToRelvarB].
If the [opt -complete] option is missing,
then [arg correlRelvar] is allowed to have a subset of the cartesian product
of the references.

Correlation constraints are often used to enforce the referential
constraints for relvars that are related in a many to many fashion.
However, they also arise in situations where the multiplicity is not
many-to-many but when the [arg correlRelvar] contains attributes that
apply to the association between [arg refToRelvarA] and [arg refToRelvarB]
themselves.

[example {
% relvar correlation C1 OWNERSHIP OwnerName + OWNER OwnerName DogName * DOG DogName
}]

[call [cmd "::ral::relvar create"] [arg relvarName] [arg heading]]

The [cmd create] subcommand creates a new relation variable whose name is given
by [arg relvarName] and that has the relation value heading given
by the [arg heading] argument.
The subcommand returns the relation value of the newly created relvar,
which necessarily will have cardinality of 0.
The [arg heading] argument is a valid relation value heading
as described for the [cmd ::ral::relation] commands or
as returned from [cmd "::ral::relation heading"].
If [arg relvarName] name begins with "::", then it is interpreted
as a fully resolved name and will be created in the given namespace.
Otherwize [arg relvarName] is interpreted as relative the to current namespace.
In either case,
an ordinary Tcl variable is also created by the same fully resolved
name as the relvar.
This implies that any namespaces referenced in [arg relvarName]
must already exist.

[example {
% relvar create OWNER {
>       Relation
>       {OwnerName string Age int City string}
>       OwnerName
>     }
Relation {OwnerName string Age int City string} OwnerName {}
% puts [relformat $::OWNER]
+=========+---+------+
|OwnerName|Age|City  |
|string   |int|string|
+=========+---+------+
+=========+---+------+
% relvar create DOG {
>       Relation
>       {DogName string Breed string}
>       DogName
>     }
Relation {DogName string Breed string} DogName {}
% relvar create OWNERSHIP {
>       Relation
>       {OwnerName string DogName string Acquired string}
>       {{OwnerName DogName}}
>     }
Relation {OwnerName string DogName string Acquired string} {{OwnerName DogName}} {}
}]

[call [cmd "::ral::relvar delete"] [arg relvarName]\
    [arg tupleVarName] [arg expression]]

The [cmd delete] subcommands deletes tuples from the relation variable
given by the [arg relvarName] argument.
Each tuple in the body of [arg relvarName] is successively
assigned to the tuple variable named [arg tupleVarName] and
[arg expression] is evaluated.
If [arg expression] returns true, then that tuple is deleted, directly
modifying the value contained in [arg relvarName].
The return value of the subcommand is the number of tuples deleted.

[example {
% puts [relformat $::OWNER "Before deleting Sue"]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
Before deleting Sue
-------------------
% relvar delete OWNER o {[tuple extract $o OwnerName] eq "Sue"}
1
% puts [relformat $::OWNER "After deleting Sue"]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
After deleting Sue
------------------
}]

[call [cmd "::ral::relvar deleteone"] [arg relvarName]\
    [opt "attr1 value1 attr2 value2 ..."]]

The [cmd deleteone] subcommand deletes at most one tuple from the
relation variable given by the [arg relvarName] argument.
The [arg "attrN valueN"] arguments must be given in pairs
and are the attribute names and values that determine which tuple
is deleted from the relation value held in [arg relvarName].
The set of attribute names in the [arg "attrN valueN"] arguments
must form one of the identifiers of [arg relvarName].
If [arg relvarName] contains a tuple whose attribute values match those
in given then that tuple is deleted and [arg relvarName]
is modified in place.
Otherwise [arg relvarName] is unchanged.
This subcommand is useful in those contexts where the attribute values of an
identifier are known and evaluating an expression over all the tuples
in [arg relvarName] is superfluous.
The return value of the subcommand is the number of tuples deleted which will
necessarily be either 1 or 0.

[example {
% puts [relformat $::OWNER "Before deleting Mike"]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
Before deleting Mike
--------------------
% relvar deleteone OWNER OwnerName Mike
1
% puts [relformat $::OWNER "After deleting Mike"]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
After deleting Mike
-------------------
% relvar deleteone OWNER OwnerName Alfonse
0
% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
}]

[call [cmd "::ral::relvar eval"] [arg arg] [opt "arg ..."]]

The [cmd eval] subcommand concatenates it arguments together and evaluates
the resulting script as a relvar transaction.
The return value of [cmd eval] is the return value of the last
command executed in the script.
At the end of the script,
the constraints associated with any relvar that was modified
by the script are evaluated.
If either the script generates an error or any of the constraints fail,
then the values of the modified relvars are returned to the values
they held before the [cmd eval] command.
It is an error to attempt to create relvars or constraints in the
script executed by [cmd eval].

[example {
% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
% relvar eval {
>       relvar insert OWNER {OwnerName Tom Age 22 City Tulsa}
> }
for association ::A1(::OWNERSHIP [+] ==> [1] ::OWNER), in relvar ::OWNER
tuple {OwnerName Tom Age 22 City Tulsa} is not referenced by any tuple

% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
% relvar eval {
>       relvar insert OWNER {OwnerName Tom Age 22 City Tulsa}
>       relvar insert OWNERSHIP {OwnerName Tom DogName Skippy Acquired 2006}
> }
for association ::A2(::OWNERSHIP [*] ==> [1] ::DOG), in relvar ::OWNERSHIP
tuple {OwnerName Tom DogName Skippy Acquired 2006} references no tuple

% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
% relvar eval {
>       relvar insert OWNER {OwnerName Tom Age 22 City Tulsa}
>       relvar insert OWNERSHIP {OwnerName Tom DogName Jumper Acquired 2006}
>       return
> }
% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
|Tom      |22 |Tulsa        |
+=========+---+-------------+
}]

[call [cmd "::ral::relvar insert"] [arg relvarName]\
    [opt "name-value-list ..."]]

The [cmd insert] subcommand inserts the tuples given by [arg name-value-list]
arguments into the value of the relation stored in [arg relvarName].
The value of [arg relvarName] is modified in place.
The return value is a relation value with the same heading as the
value held in [arg relvarName] and whose body contains the tuples as they
were actually inserted.
This gives the caller access to any attributes that might have been modified
by a relvar trace.
It is an error to attempt to insert a duplicate tuple or a tuple whose
heading does not match the heading of the value held in [arg relvarName].
The values of all attributes of the inserted tuples must be specified
with a valid value representation of the type associated with the attribute.
However, because of relvar tracing, the tuple values given in
[arg name-value-list] may not be the ones actually inserted into the relvar.

[example {
% relvar insert DOG {DogName Skippy Breed Dalmation}
Relation {DogName string Breed string} DogName {{DogName Fido Breed Poodle} {DogName Sam Breed Collie} {DogName Spot Breed Terrier} {DogName Rover Breed Retriever} {DogName Fred Breed Spaniel} {DogName Jumper Breed Mutt} {DogName Skippy Breed Dalmation}}
% relvar insert OWNER {OwnerName Tom Age 22 City Tulsa}
for association ::A1(::OWNERSHIP [+] ==> [1] ::OWNER), in relvar ::OWNER
tuple {OwnerName Tom Age 22 City Tulsa} is not referenced by any tuple

}]

[call [cmd "::ral::relvar intersect"] [arg relvarName]\
    [opt "relationValue1 relationValue2 ..."]]

The [cmd intersect] subcommand performs the relation intersection of the
relation value contained in [arg relvarName] with the given
[arg relationValueN] arguments and assigns the result back into
[arg relvarName].
This command is shorthand for:
[nl]
relvar set <relvarName> [lb]relation intersect [lb]relvar set <relvarName>[rb]
<relationValue1> ...[rb]
[nl]
The headings of each [arg relationValueN] must match that of the value
contained in [arg relvarName].
Since the intersect operation is commutative, the order of the
[arg relationValueN] arguments does not affect the result.
The return value of the command is the new relation value stored in
[arg relvarName].

[call [cmd "::ral::relvar minus"] [arg relvarName] [arg relationValue]]

The [cmd minus] subcommand performs the relation difference of the
relation value contained in [arg relvarName] with
the [arg relationValue] argument and assigns the result back into
[arg relvarName].
This command is shorthand for:
[nl]
relvar set <relvarName> [lb]relation minus [lb]relvar set <relvarName>[rb]
<relationValue1>[rb]
[nl]
The headings of [arg relationValue] must match that of the value
contained in [arg relvarName].
Note that this operation is not commutative.
The return value of the command is the new relation value stored in
[arg relvarName].

[call [cmd "::ral::relvar names"] [opt pattern]]

The [cmd names] subcommand returns a list of the currently defined
relation variables.
If the [arg pattern] argument is specified then only those names matching
[arg pattern] are returned.
Name matching is performed as for the [cmd "string match"] command.
The returned names are fully qualified.

[example {
% relvar names
::DOG ::OWNERSHIP ::OWNER
% relvar names ::D*
::DOG
}]

[call [cmd "::ral::relvar partition"] [arg name] [arg super]\
	[arg superAttrList] [arg sub1] [arg sub1AttrList]\
	[opt "sub2 sub2AttrList sub3 sub3AttrList ..."]]

The [cmd partition] subcommand defines a partition constraint named
[arg name].
The relvar named [arg super] must have as one of its identifiers the
attributes named in the [arg superAttrList] argument.
The remaining arguments define the sub set relvars named [arg subN]
and the attributes in those relvars that reference the super set relvar
are given by the [arg subNAttrList] arguments.
A partition constraint declares that every tuple in the [arg subN]
relvars references exactly one tuple in the [arg super] relvar and that
every tuple in the [arg super] relvar is referred to by exactly one
tuple contained in one of the [arg subN] relvars.
This constraint implies that the tuples in the [arg super] relvar are
completely partitioned into the disjoint sub sets given by the the
[arg subN] relvars.

[example {
% relvar create Lamp {
>       Relation
>       {SerialNo string ModelNo string Make string}
>       SerialNo
>     }
Relation {SerialNo string ModelNo string Make string} SerialNo {}
% relvar create TableLamp {
>       Relation
>       {SerialNo string Shade string}
>       SerialNo
>     }
Relation {SerialNo string Shade string} SerialNo {}
% relvar create FloorLamp {
>       Relation
>       {SerialNo string Height int Sockets int}
>       SerialNo
>     }
Relation {SerialNo string Height int Sockets int} SerialNo {}
% relvar partition P1 Lamp SerialNo TableLamp SerialNo FloorLamp SerialNo
% relvar insert Lamp {SerialNo NF100 ModelNo FCN-22  Make Falcon}
for partition ::P1(::Lamp is partitioned [::TableLamp | ::FloorLamp]), in relvar ::Lamp
tuple {SerialNo NF100 ModelNo FCN-22 Make Falcon} is not referred to by any tuple

% relvar eval {
>       relvar insert Lamp {SerialNo NF100 ModelNo FCN-22  Make Falcon}
>       relvar insert TableLamp {SerialNo NF100  Shade Blue}
>       return
>     }
% relvar insert FloorLamp {SerialNo NF100 Height 72 Sockets 3}
for partition ::P1(::Lamp is partitioned [::TableLamp | ::FloorLamp]), in relvar ::Lamp
tuple {SerialNo NF100 ModelNo FCN-22 Make Falcon} is referred to by multiple tuples

% relvar insert FloorLamp {SerialNo NF101 Height 72 Sockets 3}
for partition ::P1(::Lamp is partitioned [::TableLamp | ::FloorLamp]), in relvar ::FloorLamp
tuple {SerialNo NF101 Height 72 Sockets 3} references no tuple

}]

[call [cmd "::ral::relvar path"] [arg relvarName]]
The [cmd path] subcommand returns the fully qualified relvar
name of [arg relvarName].


[call [cmd "::ral::relvar set"] [arg relvarName] [opt relationValue]]

The [cmd set] subcommand replaces the current value held by the relation
variable named [arg relvarName] with the value given by
[arg relationValue].
It is an error to attempt to assign a relation value to a relation variable
that is of a different type than the type of the value
that the variable currently holds.
In other words, it is not possible to change the type of a relvar
by assignment.
The return value of the subcommand is the current value
held by [arg relvarName].
If the [arg relationValue] argument is missing, then no attempt is made to
change the value of [arg relvarName].
This command operates in a manner analogous to the [cmd set] command for
ordinary Tcl variables.

[example {
% relvar create PUPPY {
>       Relation
>       {PuppyName string Dame string Sire string}
>       PuppyName
>     }
Relation {PuppyName string Dame string Sire string} PuppyName {}
% relvar set PUPPY {
>       Relation
>       {PuppyName string Dame string Sire string}
>       PuppyName
>       {
>           {PuppyName Bitsy Dame Spot Sire Jumper}
>       }
>     }
Relation {PuppyName string Dame string Sire string} PuppyName {{PuppyName Bitsy Dame Spot Sire Jumper}}
% relvar set PUPPY $::DOG
headings not equal, "Relation {DogName string Breed string} DogName"
}]

[call [cmd "::ral::relvar trace"] [arg option] [arg type] [opt [arg args]]]
[list_begin definitions]
[lst_item "[cmd {::ral::relvar trace add variable}] [arg relvarName]\
    [arg traceops] [arg cmdPrefix]"]
[lst_item "[cmd {::ral::relvar trace remove variable}] [arg relvarName]\
    [arg traceops] [arg cmdPrefix]"]
[lst_item "[cmd {::ral::relvar trace info variable}] [arg relvarName]"]
[lst_item "[cmd {::ral::relvar trace add eval}] [arg cmdPrefix]"]
[lst_item "[cmd {::ral::relvar trace remove eval}] [arg cmdPrefix]"]
[lst_item "[cmd {::ral::relvar trace info eval}]"]
[list_end]

The [cmd "trace add variable"] subcommand adds a trace to [arg relvarName].
The [arg traceops] argument is a list of one or more trace operation keywords
from the list:
[list_begin definitions]
[lst_item delete]
[lst_item insert]
[lst_item set]
[lst_item unset]
[lst_item update]
[list_end]

The [cmd "trace remove variable"] subcommand deletes any trace on
[arg relvarName] that matches [arg traceops] and [arg command].

The [cmd "trace info variable"] subcommand returns the trace information
for the relvar whose name is given by [arg relvarName].
The trace information is a list each element of which describes one trace
on the relvar.
Each trace information element is in turn a list consisting of two elements,
the [arg traceops] and [arg command], of the relvar trace.

It is an error to request any trace operation if [arg relvarName] does not
exist.

The [cmd "trace add eval"], [cmd "trace remove eval"] and
[cmd "trace info eval"] variations perform the corresponding
operation for tracing transactions via the [cmd "relvar eval"] command.

See the [emph "RELVAR TRACES"] section below for further details.

[call [cmd "::ral::relvar transaction"] [arg option]]
[list_begin definitions]
[lst_item "[cmd {::ral::relvar transaction begin}]"]
[lst_item "[cmd {::ral::relvar transaction end}]"]
[lst_item "[cmd {::ral::relvar transaction rollback}]"]
[list_end]
The [cmd transaction] subcommand controls the evaluation of relvar
constraints.
Like [cmd "relvar eval"], [cmd "relvar transaction"] demarcates the
boundaries of when relvar constraints are evaluated.
[cmd "relvar transaction begin"] signals the beginning of a constraint
transaction.
When the matching [cmd "relvar transaction end"] is executed,
all contraints for all relvars modified since the corresponding
[cmd "relvar transaction begin"] command are evaluated.
Executing the [cmd "relvar transaction rollback"] command, halts the
transaction and discards all the modifications, restoring the relvar
state to that which existed before the execution of
[cmd "relvar transaction begin"].
This command is complementary to [cmd "relvar eval"] in that it provides
the same behavior but for those circumstances where accumulating the
relvar modifications into a single script is not convenient or possible.
The command returns the empty string.
If relvar modifications are such that the constraints are violated,
then [cmd "relvar transaction end"] will throw an error.


[call [cmd "::ral::relvar union"] [arg relvarName]\
    [opt "relationValue1 relationValue2 ..."]]

The [cmd union] subcommand performs the relation union of the
relation value contained in [arg relvarName] with the given
[arg relationValueN] arguments and assigns the result back into
[arg relvarName].
This command is shorthand for:
[nl]
relvar set <relvarName> [lb]relation union [lb]relvar set <relvarName>[rb]
<relationValue1> ...[rb]
[nl]
The headings of each [arg relationValueN] must match that of the value
contained in [arg relvarName].
Since the union operation is commutative, the order of the
[arg relationValueN] arguments does not affect the result.
The return value of the command is the new relation value stored in
[arg relvarName].

[call [cmd "::ral::relvar unset"] [opt "relvarName ..."]]

The [cmd unset] subcommand deletes all the relation variables whose
names are given as the subcommand arguments.
The corresponding Tcl variables that were created when the relvar
was created are also [cmd unset].
This command operates in a manner analogous to the [cmd unset] command for
ordinary Tcl variables.
It is an error to attempt to [cmd unset] a relvar that is associated
with any constraints.

[call [cmd "::ral::relvar update"] [arg relvarName] [arg tupleVarName]\
    [arg expression] [arg script]]

The [cmd update] subcommand modifies the values of tuples in the relation
value contained in the relation variable given by [arg relvarName].
Each tuple in [arg relvarName] is successively assigned to the tuple
variable given by [arg tupleVarName] and [arg expression] is evaluated.
If the result of the evaluation is true,
then [arg script] is evaluated.
The value of [arg tupleVarName] at the end of executing
[arg script] is then updated into [arg relvarName].
Typically, [arg script] will invoke [cmd "::ral::tuple update"]
to modify the value of [arg tupleVarName].
The return value of the subcommand
is a relation value with the same heading as the
value held in [arg relvarName] and whose body contains the tuples as they
were actually updated.
This gives the caller access to any attributes that might have been modified
by a relvar trace.
If the [arg script] throws an error or the modified value of a tuple cannot
be updated into [arg relvarName], all changes to [arg relvarName] are
discarded and the value of [arg relvarName] is restored to its previous value.
Should [arg script] execute a [cmd break] or [cmd return] command,
then [arg relvarName] is updated (if possible) and the return code
is propagated to any surrounding command.


[example {
% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
% relvar update OWNER o {[tuple extract $o OwnerName] eq "Sue"} {
>       tuple update o Age [expr {[tuple extract $o Age] + 1}]
>     }
1
% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |25 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
% relvar update OWNER o {[tuple extract $o OwnerName] eq "George"} {
>       tuple update o OwnerName Alfonse
>     }
for association ::A1(::OWNERSHIP [+] ==> [1] ::OWNER), in relvar ::OWNER
tuple {OwnerName Alfonse Age 35 City Sunnyvale} is not referenced by any tuple
for association ::A1(::OWNERSHIP [+] ==> [1] ::OWNER), in relvar ::OWNERSHIP
tuple {OwnerName George DogName Fido Acquired 2001} references no tuple
tuple {OwnerName George DogName Sam Acquired 2000} references no tuple

% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |25 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
}]

[call [cmd "::ral::relvar updateone"] [arg relvarName] [arg tupleVarName]\
    [arg id-name-value-list] [arg script]]

The [cmd updateone] subcommand modifies at most one tuple from the
relation variable given by the [arg relvarName] argument.
The [arg id-name-value-list] must be list containing an even number of elements
which are attribute names alternating with attribute values.
The set of attribute names in [arg id-name-value-list] must form one of the
identifiers of [arg relvarName].
If [arg relvarName] contains a tuple whose attribute values match those
in [arg name-value-list] then
that tuple is assigned to [arg tupleVarName] and [arg script] is executed.
After executing [arg script] the value contained in [arg tupleVarName]
replaces the original tuple, modifying [arg relvarName] in place.
Typically, [arg script] will invoke [cmd "::ral::tuple update"]
to modify the value of [arg tupleVarName].
If no matching tuple is found, then [arg relvarName] is unchanged.
This subcommand is useful in those contexts where the attribute values of an
identifier are known and evaluating an expression over all the tuples
in [arg relvarName] is superfluous.
The return value is a relation value with the same heading as the
value held in [arg relvarName] and whose body contains either the single
tuple that was updated or is empty if no matching tuple was found.
This give the caller access to any attributes that might have been modified
by a relvar trace.

[example {
% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |35 |Sunnyvale    |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
% relvar updateone OWNER o {OwnerName George} {
> tuple update o Age 37 City {New York}
> }
1
% puts [relformat $::OWNER]
+=========+---+-------------+
|OwnerName|Age|City         |
|string   |int|string       |
+=========+---+-------------+
|Sue      |24 |Cupertino    |
|George   |37 |New York     |
|Alice    |30 |San Jose     |
|Mike     |50 |San Jose     |
|Jim      |42 |San Francisco|
+=========+---+-------------+
}]

[list_end]

[section "RELVAR TRACES"]

By analogy to tracing ordinary variables in Tcl,
relvars can also be traced.
However, relvar tracing must account for the
operations that are applied to relvars.
There are five operations that may be traced on the relation values
held in relvars and one operation that allows tracing of transactions
on relvars.
For these operations, if a command prefix has been registered using
[cmd "relvar trace add variable"] or [cmd "relvar trace add eval"]
then that command prefix will have
additional arguments appended and will be invoked as described below.
Relvar traces operate in a very similar fashion as traces on ordinary
Tcl variables.
The trace command is invoked when the corresponding relvar operation
is invoked and is invoked in the context of the caller of the relvar operation,
which, in general, is different from the caller of the [cmd "relvar trace add"]
command.
When executing a variable type trace for a relvar,
all other variable type tracing for that relvar is inhibited.
If multiple traces have been added to a relvar,
the traces are invoked in the order of most recent first to oldest last.

[list_begin definitions]
[lst_item delete]
A delete trace is invoked for each tuple in a relvar that is deleted.
Tuples are deleted from relvars by invocations of [cmd "relvar delete"]
or [cmd "relvar deleteone"].
The delete trace command is invoked as:
[nl]
[cmd cmdPrefix] [cmd delete] [arg relvarName] [arg tupleValue]
[nl]
where [cmd cmdPrefix] is the command prefix of the trace as given
in [cmd "relvar trace add variable"],
the string [cmd delete] indicates that a tuple is to be deleted from the relvar,
[arg relvarName] is the fully qualified name of the relvar
and [arg tupleValue] is the value of the tuple about to be deleted.
The trace command is invoked before the tuple is actually removed from
the relation value held in the relvar.
If [cmd cmdPrefix] throws an error, then no other delete traces in the chain of
delete traces for the relvar are invoked and [arg tupleValue] is not removed
from the relvar value.

[lst_item insert]
An insert trace is invoked for each tuple in a relvar that is inserted.
Tuples are inserted into relvars by invocations of [cmd "relvar insert"].
The insert trace command is invoked as:
[nl]
[cmd cmdPrefix] [cmd insert] [arg relvarName] [arg tupleValue]
[nl]
where [cmd cmdPrefix] is the command prefix of the trace as given
in [cmd "relvar trace add variable"],
the string [cmd insert] indicates that a tuple
is to be inserted into the relvar,
[arg relvarName] is the fully qualified name of the relvar
and [arg tupleValue] is the value of the tuple about to be inserted
The trace command is invoked before the tuple is actually inserted into
the relation value held in the relvar.
The [cmd cmdPrefix] command must return a Tuple value.
The returned Tuple value is passed along to the next insert trace command.
The returned Tuple from the last trace command in the chain is then
inserted into the relvar.
Insert trace commands may choose to modify the value of [arg tupleValue] but
are required to return a value that can be coerced into a Tuple type and the
heading of the returned Tuple of the last trace command in the chain
must match that of the relvar if it is to be successfully inserted.
If [cmd cmdPrefix] throws an error, then no other insert traces in the chain of
insert traces for the relvar are invoked and [arg tupleValue] is not inserted
into the relvar value.

[lst_item set]
A set trace is invoked each time that the relation value held in a relvar
is assigned.
Relations values are assigned to relvars by invocation of the
[cmd "relvar set"],
[cmd "relvar union"],
[cmd "relvar intersect"]
or
[cmd "relvar minus"]
command.
The set trace command is invoked as:
[nl]
[cmd cmdPrefix] [cmd set] [arg relvarName] [arg relationValue]
[nl]
where [cmd cmdPrefix] is the command prefix of the trace as given
in [cmd "relvar trace add variable"],
the string [cmd set] indicates that a relation value
is to be assigned to the relvar,
[arg relvarName] is the fully qualified
name of the relvar
and [arg relationValue] is the relation value that is about to be assigned
to the relvar.
The trace command is invoked before the relation is actually assigned to the
relvar.
The [cmd cmdPrefix] command must return a Relation value.
The returned Relation value is passed along to the next set trace command.
The returned Relation from the last trace command in the chain is then
assigned to the relvar.
Set trace commands may choose to modify the value of [arg relationValue] but
are required to return a value that can be coerced into a Relation type and the
heading of the returned Relation of the last trace command in the chain
must match that of the relvar if it is to be successfully assigned
If [cmd cmdPrefix] throws an error, then no other set traces in the chain of
set traces for the relvar are invoked and [arg relationValue] is not assigned
to the relvar.

[lst_item unset]
An unset trace is invoked each time a relvar is destroyed.
Relvars are destroyed by the invocation of the [cmd "relvar unset"] command.
Note that due to a limitation of Tcl,
relvar unset traces are not invoked when an interpreter is deleted.
The unset trace command is invoked as:
[nl]
[cmd cmdPrefix] [cmd unset] [arg relvarName]
[nl]
where [cmd cmdPrefix] is the command prefix of the trace as given
in [cmd "relvar trace add variable"],
the string [cmd unset] indicates that the relvar
is being destroyed,
and [arg relvarName] is the fully qualified
name of the relvar.
The trace command is invoked before the relvar is actually destroyed so
the relation value stored in the relvar is still intact.
Any errors thown by [cmd cmdPrefix] are ignored.

[lst_item update]
An update trace is invoked for each tuple in a relvar that is updated.
Tuples are updated into relvars by invocations of [cmd "relvar update"]
or [cmd "relvar updateone"].
The update trace command is invoked as:
[nl]
[cmd cmdPrefix] [cmd update] [arg relvarName] [arg oldTupleValue]
    [arg newTupleValue]
[nl]
where [cmd cmdPrefix] is the command prefix of the trace as given
in [cmd "relvar trace add variable"],
the string [cmd update] indicates that a tuple
is to be updated in the relvar,
[arg relvarName] is the fully qualified name of the relvar,
[arg oldTupleValue] is the current value of the tuple in the relvar
and [arg newTupleValue] is the value that the tuple will have when
the update is performed.
The trace command is invoked before the tuple is actually updated in
the relation value held in the relvar.
The [cmd cmdPrefix] command must return a Tuple value.
The returned Tuple value is passed along to the next update trace command.
The returned Tuple from the last trace command in the chain is then
updated into the relvar.
Update trace commands may choose to modify the value of [arg newTupleValue] but
are required to return a value that can be coerced into a Tuple type and the
heading of the returned Tuple of the last trace command in the chain
must match that of the relvar if it is to be successfully updated.
If [cmd cmdPrefix] throws an error, then no other update traces in the chain of
update traces for the relvar are invoked and [arg tupleValue] is not updated
into the relvar value.

[lst_item eval]
An eval trace is invoked each time that a [cmd "relvar eval"] begins and
ends or when [cmd "relvar transaction"] commands are invoked.
Eval traces are different from other relvar traces in that they provide a
means of determining the boundaries of a transaction rather than specific
operations on a relvar.
The eval trace command is invoked as:
[nl]
[cmd cmdPrefix] [cmd eval] [arg op] [arg level]
[nl]
where [cmd cmdPrefix] is the command prefix of the trace as given
in [cmd "relvar trace add eval"], 
the string [cmd eval] indicates that the relvar transaction is in progress,
the [arg op] is either the string "begin" or "end" indicating whether
or not the [cmd "relvar eval"] is just starting or is ending and
[arg level] is an integer greater than zero indicating the nesting depth
of the transaction.
The eval trace is invoked with the "begin" op before the script associated
with a [cmd "relvar eval"] is executed and is invoked with the "end" op
after the script is executed but before the relvar constraints are evaluated
for the transaction.
Any errors thown by [cmd cmdPrefix] are ignored.

[list_end]

[see_also tuple relation]

[keywords relation variable]

[manpage_end]
