#!/bin/sh
# \
exec tclsh "$0" "$@"
# This software is copyrighted 2006 by G. Andrew Mangogna.
# The following terms apply to all files associated with the software unless
# explicitly disclaimed in individual files.
# 
# The authors hereby grant permission to use, copy, modify, distribute,
# and license this software and its documentation for any purpose, provided
# that existing copyright notices are retained in all copies and that this
# notice is included verbatim in any distributions. No written agreement,
# license, or royalty fee is required for any of the authorized uses.
# Modifications to this software may be copyrighted by their authors and
# need not follow the licensing terms described here, provided that the
# new terms are clearly indicated on the first page of each file where
# they apply.
# 
# IN NO EVENT SHALL THE AUTHORS OR DISTRIBUTORS BE LIABLE TO ANY PARTY FOR
# DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES ARISING
# OUT OF THE USE OF THIS SOFTWARE, ITS DOCUMENTATION, OR ANY DERIVATIVES
# THEREOF, EVEN IF THE AUTHORS HAVE BEEN ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.
# 
# THE AUTHORS AND DISTRIBUTORS SPECIFICALLY DISCLAIM ANY WARRANTIES,
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE, AND NON-INFRINGEMENT.  THIS SOFTWARE
# IS PROVIDED ON AN "AS IS" BASIS, AND THE AUTHORS AND DISTRIBUTORS HAVE
# NO OBLIGATION TO PROVIDE MAINTENANCE, SUPPORT, UPDATES, ENHANCEMENTS,
# OR MODIFICATIONS.
# 
# GOVERNMENT USE: If you are acquiring this software on behalf of the
# U.S. government, the Government shall have only "Restricted Rights"
# in the software and related documentation as defined in the Federal
# Acquisition Regulations (FARs) in Clause 52.227.19 (c) (2).  If you
# are acquiring the software on behalf of the Department of Defense,
# the software shall be classified as "Commercial Computer Software"
# and the Government shall have only "Restricted Rights" as defined in
# Clause 252.227-7013 (c) (1) of DFARs.  Notwithstanding the foregoing,
# the authors grant the U.S. Government and others acting in its behalf
# permission to use and distribute the software in accordance with the
# terms specified in this license.
# 
#  *++
# MODULE:
#   trace.test -- test cases for relvar tracing the Tcl Relational Algebra
#   Library
# 
# ABSTRACT:
# 
# $RCSfile: trace.test,v $
# $Revision: 1.10.2.2 $
# $Date: 2009/02/15 23:34:59 $
#  *--

package require tcltest 2.2
package require ral
package require ralutil

eval tcltest::configure $argv

proc tupleMatch {expected actual} {
    return [::ral::tuple equal $expected $actual]
}

proc relationMatch {expected actual} {
    return [::ral::relation is $expected == $actual]
}

namespace eval ::ral::test {
    namespace import ::tcltest::*
    namespace import ::ral::*

    customMatch tuple tupleMatch
    customMatch relation relationMatch

    set recNum 0
    array set idAttrs {}
    ralutil::sysIdsInit

    proc traceProc {op relvarName args} {
	variable traceInfo
	set traceInfo [lindex $args end]
    }

    proc constAttr {attr op relvarName tup} {
	tuple update tup $attr 1
	return $tup
    }

    proc incrAttr {attrName op relvarName tup} {
	tuple update tup $attrName [expr {[tuple extract $tup $attrName] + 1}]
	return $tup
    }

    proc modTuple {attrName attrValue op relvarName tup} {
	::ral::tuple update tup $attrName $attrValue
	return $tup
    }

    set S [relation create\
	{S# string SNAME string STATUS int CITY string}\
	{S# S1 SNAME Smith STATUS 20 CITY London}\
	{S# S2 SNAME Jones STATUS 10 CITY Paris}\
	{S# S3 SNAME Blake STATUS 30 CITY Paris}\
	{S# S4 SNAME Clark STATUS 20 CITY London}\
	{S# S5 SNAME Adams STATUS 30 CITY Athens}\
    ]

    # relvar trace subcommand
    test relvar-trace-1.0 {unknown relvar} -body {
	relvar trace add variable foo insert traceProc
    } -result {unknown relvar name, "foo"} -returnCodes error

    test relvar-trace-1.1 {bad arguments} -setup {
	relvar create SV {S# string SNAME string STATUS int CITY string} S#
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV insert
    } -result {wrong # args: should be "relvar trace add variable relvarName ops cmdPrefix"} -returnCodes error

    test relvar-trace-1.2 {bad trace operation} -setup {
	relvar create SV {S# string SNAME string STATUS int CITY string} S#
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV foo traceProc
    } -result {bad traceOp "foo": must be delete, insert, set, unset, or update} -returnCodes error

    test relvar-trace-2.0 {single operation} -setup {
	relvar create SV {S# string SNAME string STATUS int CITY string} S#
	relvar set SV $S
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV insert traceProc
	relvar trace info variable SV
    } -result {{insert traceProc}}

    test relvar-trace-2.1 {multiple operations} -setup {
	relvar create SV {S# string SNAME string STATUS int CITY string} S#
	relvar set SV $S
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV {insert delete update} traceProc
	relvar trace info variable SV
    } -result {{{delete insert update} traceProc}}

    test relvar-trace-3.0 {remove trace} -setup {
	relvar create SV {S# string SNAME string STATUS int CITY string} S#
	relvar set SV $S
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV {insert delete update} traceProc
	relvar trace add variable SV {set} traceProc
	relvar trace add variable SV {unset} traceProc
	relvar trace remove variable SV {delete insert update} traceProc
	relvar trace info variable SV
    } -result {{unset traceProc} {set traceProc}}

    test relvar-trace-4.0 {insert tracing} -setup {
	relvar create SV {S# string SNAME string STATUS int CITY string} S#
	relvar set SV $S
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV insert traceProc
	relvar insert SV {S# S6 SNAME Foo STATUS 40 CITY Bar}
	set traceInfo
    } -result {
        {S# string SNAME string STATUS int CITY string}
        {S# S6 SNAME Foo STATUS 40 CITY Bar}
    } -match tuple

    test relvar-trace-4.1 {modify inserted tuple} -setup {
	relvar create SV {S# string SNAME string STATUS int CITY string} S#
	relvar set SV $S
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV insert [list modTuple SNAME Baz]
	relvar insert SV {S# S6 SNAME Foo STATUS 40 CITY Bar}
    } -result {
	{S# string SNAME string STATUS int CITY string}
	{{S# S6 SNAME Baz STATUS 40 CITY Bar}}
    } -match relation

    test relvar-trace-4.2 {modify inserted tuple several times} -setup {
	relvar create SV {S# string SNAME string STATUS int CITY string} S#
	relvar set SV $S
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV insert [list incrAttr STATUS]
	relvar trace add variable SV insert [list incrAttr STATUS]
	relvar trace add variable SV insert [list incrAttr STATUS]
	relvar insert SV {S# S6 SNAME Foo STATUS 40 CITY Bar}
    } -result {
	{S# string SNAME string STATUS int CITY string}
	{{S# S6 SNAME Foo STATUS 43 CITY Bar}}
    } -match relation

    test relvar-trace-4.3 {
	use tracing to create unique identifiers
    } -setup {
	relvar create SV {S# int SNAME string STATUS int CITY string} S#
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV insert {::ralutil::sysIdsCreateIdFor S#}
	relvar insert SV {S# {} SNAME John STATUS 40 CITY Bar}
	relvar insert SV {S# {} SNAME John STATUS 40 CITY Bar}
    } -result {
	{S# int SNAME string STATUS int CITY string}
	{
	    {S# 2 SNAME John STATUS 40 CITY Bar}
	}
    } -match relation

    test relvar-trace-4.3 {
	trace proc creates a duplicate tuple
    } -setup {
	relvar create SV {S# int SNAME string STATUS int CITY string} S#
    } -cleanup {
	relvar unset SV
    } -body {
	relvar trace add variable SV insert {constAttr S#}
	relvar insert SV {S# {} SNAME John STATUS 40 CITY Bar}
	relvar insert SV {S# {} SNAME John STATUS 40 CITY Bar}
    } -result {duplicate tuple, "{S# int SNAME string STATUS int CITY string} {S# 1 SNAME John STATUS 40 CITY Bar}"} -returnCodes error

    proc insertMap {relvarName opsList tup} {
	variable recNum
	#puts "insertMap: $relvarName, $opsList\n[tupleformat $tup\
	    "Insert Tuple"]"
	relvar eval {
	    relvar insert SMap [list R# $recNum S# [tuple extract $tup S#]]
	}
	incr recNum
	return $tup
    }

    proc deleteMap {relvarName opsList tup} {
	#puts "deleteMap: $relvarName, $opsList\n[tupleformat $tup\
	    "Delete Tuple"]"
	relvar eval {
	    relvar deleteone SMap S# [tuple extract $tup S#]
	}
    }

    proc updateMap {relvarName opsList oldtup newtup} {
	#puts [format "updateMap: $relvarName, $opsList\n%s\n%s"\
	    [tupleformat $oldtup "Old Tuple"]\
	    [tupleformat $newtup "New Tuple"]]
	set oldS [tuple extract $oldtup S#]
	set newS [tuple extract $newtup S#]
	if {$oldS ne $newS} {
	    relvar eval {
		relvar updateone SMap sm [list S# $oldS] {
		    tuple update sm S# $newS
		}
	    }
	}
	return $newtup
    }

    proc setMap {relvarName opsList rel} {
	#puts "setMap: $relvarName, $opsList\n[relformat $rel setMap]"
	set newR [relation tag [relation project $rel S#] R# -ascending S#]
	relvar eval {
	    relvar set SMap $newR
	}
	variable recNum [relation cardinality $newR]
	return $rel
    }

    proc unsetMap {relvarName opsList} {
	relvar set SMap [relation emptyof [relvar set SMap]]
    }

    unset S
    relvar create S {S# string SNAME string STATUS int CITY string} S#

    relvar create SMap {R# int S# string} R# S#

    test relvar-trace-5.0 {insert tracing} -setup {
	relvar trace add variable S insert [namespace code insertMap]
    } -cleanup {
	relvar trace remove variable S insert [namespace code insertMap]
    } -body {
	relvar insert S\
	    {S# S1 SNAME Smith STATUS 20 CITY London}\
	    {S# S2 SNAME Jones STATUS 10 CITY Paris}\
	    {S# S3 SNAME Blake STATUS 30 CITY Paris}\
	    {S# S4 SNAME Clark STATUS 20 CITY London}
	relvar set SMap
    } -result {
	{R# int S# string}
	{
	    {R# 0 S# S1}
	    {R# 1 S# S2}
	    {R# 2 S# S3}
	    {R# 3 S# S4}
	}
    } -match relation

    test relvar-trace-5.1 {traces present for different operations} -setup {
	relvar trace add variable S insert [namespace code insertMap]
	relvar trace add variable S delete [namespace code deleteMap]
    } -cleanup {
	relvar trace remove variable S insert [namespace code insertMap]
    } -body {
	relvar insert S\
	    {S# S5 SNAME Adams STATUS 30 CITY Athens}
	relvar set SMap
    } -result {
	{R# int S# string}
	{
	    {R# 0 S# S1}
	    {R# 1 S# S2}
	    {R# 2 S# S3}
	    {R# 3 S# S4}
	    {R# 4 S# S5}
	}
    } -match relation

    test relvar-trace-5.2 {delete tracing} -setup {
	relvar trace add variable S delete [namespace code deleteMap]
    } -cleanup {
	relvar trace remove variable S delete [namespace code deleteMap]
    } -body {
	relvar deleteone S S# S2
	relvar set SMap
    } -result {
	{R# int S# string}
	{
	    {R# 0 S# S1}
	    {R# 2 S# S3}
	    {R# 3 S# S4}
	    {R# 4 S# S5}
	}
    } -match relation

    test relvar-trace-5.3 {update tracing} -setup {
	relvar trace add variable S update [namespace code updateMap]
    } -cleanup {
	relvar trace remove variable S update [namespace code updateMap]
    } -body {
	relvar updateone S supp {S# S3} {
	    tuple update supp S# S6
	}
	relvar set SMap
    } -result {
	{R# int S# string}
	{
	    {R# 0 S# S1}
	    {R# 2 S# S6}
	    {R# 3 S# S4}
	    {R# 4 S# S5}
	}
    } -match relation

    test relvar-trace-5.4 {set tracing} -setup {
	relvar trace add variable S set [namespace code setMap]
    } -cleanup {
	relvar trace remove variable S set [namespace code setMap]
    } -body {
	relvar set S [relation insert $S {S# S7 SNAME Foo STATUS 25 CITY Bar}]
	relvar set SMap
    } -result {
	{R# int S# string}
	{
	    {R# 0 S# S1}
	    {R# 1 S# S4}
	    {R# 2 S# S5}
	    {R# 3 S# S6}
	    {R# 4 S# S7}
	}
    } -match relation

    test relvar-trace-5.5 {unset tracing} -setup {
	relvar trace add variable S unset [namespace code unsetMap]
    } -cleanup {
    } -body {
	relvar unset S
	relvar set SMap
    } -result {
	{R# int S# string}
	{}
    } -match relation

    relvar create S {S# string SNAME string STATUS int CITY string} S#
    relvar insert S\
	{S# S1 SNAME Smith STATUS 20 CITY London}\
	{S# S2 SNAME Jones STATUS 10 CITY Paris}\
	{S# S3 SNAME Blake STATUS 30 CITY Paris}\
	{S# S4 SNAME Clark STATUS 20 CITY London}

    proc evalTrace {op begin level} {
	variable evalInfo
	lappend evalInfo [list $op $begin $level]
    }

    test relvar-trace-6.1 {eval tracing} -setup {
	relvar trace add eval [namespace code evalTrace]
    } -cleanup {
	relvar trace remove eval [namespace code evalTrace]
    } -body {
	relvar eval {
	}
	set evalInfo
    } -result {{eval begin 1} {eval end 1}}

    cleanupTests
}

namespace delete ::ral::test
